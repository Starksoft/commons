apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {

	compileSdkVersion buildConfig.compileSdk
	buildToolsVersion versions.buildTools

	defaultConfig {
		minSdkVersion buildConfig.minSdk
		targetSdkVersion buildConfig.targetSdk

		versionCode 1
		versionName buildConfig.version
		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}

ext.artifact = [
		'groupId': 'ru.starksoft',
		'id'     : 'commons',
		'version': '1.2.0',
		'siteUrl': 'https://github.com/Starksoft/commons',
		'gitUrl' : 'https://github.com/Starksoft/commons.git',
		'repo'   : 'android-maven'
]

version = artifact.version
group = artifact.groupId

install {
	repositories.mavenInstaller {
		pom.project {
			packaging 'aar'
			groupId artifact.groupId
			artifactId artifact.id
			version artifact.version
			name artifact.id // pom.project.name must be same as bintray.pkg.name
			url artifact.siteUrl
			inceptionYear '2019' // HARDCODED
			licenses {
				license { // HARDCODED
					name 'GPL-3.0'
					url 'https://www.gnu.org/licenses/gpl.txt'
					distribution 'repo'
				}
			}
			scm {
				connection artifact.gitUrl
				developerConnection artifact.gitUrl
				url artifact.siteUrl
			}
		}
	}
}

task sourcesJar(type: Jar) {
	from android.sourceSets.main.java.srcDirs
	classifier = "sources"
}

task javadoc(type: Javadoc) {
	source = android.sourceSets.main.java.srcDirs
	classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = "javadoc"
	from javadoc.destinationDir
	// options.encoding = ‘UTF-8’
}

artifacts {
	archives sourcesJar
//	archives javadocJar
}

bintray {
	user = System.getenv('user')
	key = System.getenv('key')

	configurations = ['archives']
	pkg {
		repo = artifact.repo
		name = artifact.id
		websiteUrl = artifact.siteUrl
		vcsUrl = artifact.gitUrl
		userOrg = artifact.userOrg
		licenses = ['Apache-2.0']
		publish = true
		version {
			name = artifact.version
		}
	}
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
	implementation 'androidx.appcompat:appcompat:1.1.0'
	implementation 'androidx.core:core-ktx:1.1.0'
	testImplementation 'junit:junit:4.12'
	testImplementation "org.mockito:mockito-core:2.24.5"
	androidTestImplementation 'androidx.test.ext:junit:1.1.1'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

	implementation "androidx.appcompat:appcompat:1.1.0"
	implementation "com.google.android.material:material:1.0.0"

	implementation "io.reactivex.rxjava2:rxjava:2.2.11"
	implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

	api "dev.chrisbanes:insetter:0.2.0"
	api "dev.chrisbanes:insetter-ktx:0.2.0"
}
